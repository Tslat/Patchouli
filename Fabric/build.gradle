plugins {
	id 'fabric-loom' version '1.4.+'
}

repositories {
	maven {
		name = "Modmuss"
		url = "https://maven.modmuss50.me/"
		content {
			includeGroup "me.zeroeightsix"
		}
	}
	maven {
		name = "Shedaniel"
		url = "https://maven.shedaniel.me/"
		content {
			includeGroup "me.shedaniel"
			includeGroup "me.shedaniel.cloth"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"

	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-${mappings_mc_version}:${parchment_version}@zip")
	}

	modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

	modImplementation include("me.zeroeightsix:fiber:${fiber_version}")

	modCompileOnly include("me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}") { transitive = false }
	modCompileOnly include("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") { transitive = false }

	if (project.hasProperty("jei_version"))
		modCompileOnly "mezz.jei:jei-1.20.1-common-api:${project.jei_version}"
}

loom {
	//accessWidenerPath = project(":Common").file("src/main/resources/${mod_id}.accesswidener")
	mixin.defaultRefmapName.convention("${mod_id}.refmap.json")

	runs {
		client {
			client()
			setConfigName("Fabric Client")
			ideConfigGenerated(true)
			runDir("run")
		}

		server {
			server()
			setConfigName("Fabric Server")
			ideConfigGenerated(true)
			runDir("run")
		}

		datagen {
			server()
			setConfigName("Fabric DataGen")
			ideConfigGenerated(true)
			runDir("build/datagen")

			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
			vmArg "-Dfabric-api.datagen.modid=${mod_id}"
		}
	}
}